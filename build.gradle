plugins {
    id 'org.unbroken-dome.test-sets' version '3.0.1' apply false
    id 'maven-publish'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.unbroken-dome.test-sets'
    apply plugin: 'maven-publish'

    group = 'com.vivier_technologies'
    version = '0.1.0'

    test {
        useJUnitPlatform()
    }

    testSets {
        integrationTest
    }

    dependencies {
        // This dependency is exported to consumers, that is to say found on their compile classpath.
        // api 'google.'

        // This dependency is used internally, and not exposed to consumers on their own compile classpath.
        implementation 'com.google.inject:guice:4.2.3'
        implementation 'org.apache.commons:commons-configuration2:2.7'
        implementation 'commons-beanutils:commons-beanutils:1.9.4'
        implementation 'commons-cli:commons-cli:20040117.000000'

        // Use JUnit test framework
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
        testImplementation 'org.mockito:mockito-core:3.3.3'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

        integrationTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
        integrationTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    }

    jar {
        manifest {
            attributes('Implementation-Title': project.name, 'Implementation-Version': project.version)
        }
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test

    //ensures integration test always run even when not changed
    //project.integrationTest {
    //    outputs.upToDateWhen { false }
    //}

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                pom {
                    name = "Event Sequencer"
                    description = "A high performance sequenced message stream implementation for event sourcing architectures"
                    url = "https://github.com/buckersew/sequencer"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "buckersew"
                            name = "Ed Buckney"
                            email = "vivier.technologies@gmail.com"
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/buckersew/sequencer.git'
                        developerConnection = 'scm:git:https://github.com/buckersew/sequencer.git'
                        url = 'https://github.com/buckersew/sequencer.git'
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/buckersew/sequencer"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}

project(':sequencer') {
    dependencies {
        implementation project(':utils'), project(':messages')
    }
}

project(':messages') {
    dependencies {
        implementation project(':utils')
    }
}

project(':client') {
    dependencies {
        implementation project(':utils'), project(':messages')
    }
}

